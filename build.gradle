plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
}

group 'com.github.theapache64'
version '0.0.2'

repositories {
    jcenter()
    mavenCentral()
}

test {
    useJUnitPlatform()
    environment("PLAY_API_GOOGLE_USERNAME", System.getenv("PLAY_API_GOOGLE_USERNAME"))
    environment("PLAY_API_GOOGLE_PASSWORD", System.getenv("PLAY_API_GOOGLE_PASSWORD"))
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // Akdeniz Deps
    implementation 'com.google.protobuf:protobuf-java:3.14.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpcore:4.4.14'

    // Bouncy Castle Provider : The Bouncy Castle Crypto package is a Java implementation of cryptographic algorithms.
	// This jar contains JCE provider and lightweight API for the Bouncy Castle Cryptography
	// APIs for JDK 1.5 and up.
    implementation 'org.bouncycastle:bctls-jdk15on:1.57'

    // Kotlinx Coroutines Core : Coroutines support libraries for Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
    implementation 'org.junit.jupiter:junit-jupiter:5.4.2'

    // Expekt : An assertion library for Kotlin
    testImplementation 'com.theapache64:expekt:0.0.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "shifarshifz"
            name "shifarshifz"
            email "theapache64@gmail.com"
        }
    }

    scm {
        url "https://github.com/theapache64/google-play-api"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.github.theapache64'
            artifactId 'google-play-api'
            version "$version"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'To access play store')
                root.appendNode('name', 'google-play-api')
                root.appendNode('url', 'https://github.com/theapache64/google-play-api')
                root.children().last() + pomConfig
            }
        }
    }
}


